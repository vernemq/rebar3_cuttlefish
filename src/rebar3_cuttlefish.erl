-module(rebar3_cuttlefish).
-behaviour(provider).

-export([init/1
        ,do/1
        ,format_error/1]).

-define(PROVIDER, cuttlefish).
-define(DEPS, [release]).

%% ===================================================================
%% Public API
%% ===================================================================
-spec init(rebar_state:t()) -> {ok, rebar_state:t()}.
init(State) ->
    Provider = providers:create([
            {name, ?PROVIDER},            % The 'user friendly' name of the task
            {module, ?MODULE},            % The module implementation of the task
            {bare, true},                 % The task can be run by the user, always true
            {deps, ?DEPS},                % The list of dependencies
            {example, "rebar3 cuttlefish"}, % How to use the plugin
            {opts, []},                   % list of options understood by the plugin
            {short_desc, "Rebar3 cuttlefish plugin"},
            {desc, ""}
    ]),
    {ok, rebar_state:add_provider(State, Provider)}.


-spec do(rebar_state:t()) -> {ok, rebar_state:t()} | {error, string()}.
do(State) ->
    rebar_api:info("Running cuttlefish schema generator", []),
    Relx = rebar_state:get(State, relx, []),
    ReleaseDir = filename:join(rebar_dir:base_dir(State), "rel"),

    {release, {Name, Vsn}, _} = lists:keyfind(release, 1, Relx),
    TargetDir = filename:join([ReleaseDir, Name]),
    case lists:keyfind(overlay, 1, Relx) of
        {overlay, Overlays} when is_list(Overlays) ->
            Schemas = schemas(Overlays, TargetDir),

            case cuttlefish_schema:files(Schemas) of
                {errorlist, _Es} ->
                    %% These errors were already printed
                    error;
                {_Translations, Mappings, _Validators} ->
                    make_default_file(State, Name, TargetDir, Mappings)
            end,
            migrate_cuttlefish_bin(State, ReleaseDir, atom_to_list(Name), Vsn),
            {ok, State};
        false ->
            migrate_cuttlefish_bin(State, ReleaseDir, atom_to_list(Name), Vsn),
            {ok, State};
        _ ->
            {error, {?MODULE, {bad_overlay_entry}}}

    end.

-spec format_error(any()) ->  iolist().
format_error({bad_overlay_entry}) ->
    io_lib:format("{overlay, [...]} entry in relx config must be a list.~n", []).

make_default_file(State, Name, TargetDir, Mappings) ->
    File = rebar_state:get(State, cuttlefish_filename, io_lib:format("~s.conf", [Name])),
    Filename = filename:join([TargetDir, "etc", File]),
    cuttlefish_conf:generate_file(Mappings, Filename).

schemas(Overlays, TargetDir) ->
    SchemaOverlays =
        lists:filter(fun(Overlay) ->
                         element(1, Overlay) =:= template
                             andalso filename:extension(element(3, Overlay)) =:= ".schema"
                     end, Overlays),

    lists:sort([lists:flatten(filename:join(TargetDir, element(3, Schema)))
               || Schema <- SchemaOverlays]).

%% migrate cuttlefish into relx shell
migrate_cuttlefish_bin(State, ReleaseDir, Name, Vsn) ->
    BinDir = filename:join([ReleaseDir,Name,"bin"]),
    Bins = [filename:join([BinDir,Name]), filename:join([BinDir, Name++"-"++Vsn])],
    case load_cuttlefish_bin(State, Name) of
        Data when is_binary(Data) ->
            %% try migrate cuttlefish and relx
            case lists:member(ok, [migrate_bin(N, Data) || N <- Bins]) of
                %% try copy cuttlefish script when migration succeed
                true ->
                    copy_cuttlfish_bin(State, BinDir);
                _ ->
                    ok
            end;
        Error ->
            Error
    end.

%% do migrate
migrate_bin(FileName, Data) ->  
    case file:read_file(FileName) of
        {ok, OldData} ->
            %% ensure it's generated by relx 
            case binary:match(OldData, [<<"#!/bin/sh">>,<<"$RELX_CONFIG_PATH">>,<<"$VMARGS_PATH">>]) of
                {_, 3} ->
                    file:write_file(FileName, <<Data/binary, OldData/binary>>);
                _ -> error
            end;
        {error, Reason} ->
            rebar_api:warn("read file failed: ~p~n", [Reason]),
            error
    end.

%% load template shell command
load_cuttlefish_bin(_State, Name)->
	Path = filename:dirname(filename:dirname(code:which(?MODULE))),
    FileName = filename:join([Path, "priv", "cuttlefish_cmd"]),
    case file:read_file(FileName) of
        {ok, Data} ->
            binary:replace(Data, <<"{{release_name}}">>, list_to_binary(Name));
        _ ->
            rebar_api:warn("load cuttlefish bin templeate failed: ~p~n", [FileName]),
            error
    end.

%% try to copy cuttlefish escript to release dir
copy_cuttlfish_bin(State, BinDir) ->
    TargetPath = filename:join([BinDir, "cuttlefish"]),
    SourcePath = filename:join([rebar_dir:base_dir(State), "bin", "cuttlefish"]),
    case {filelib:is_file(TargetPath), filelib:is_file(SourcePath)} of
        {false, true} ->
            file:copy(SourcePath, TargetPath);
        _ ->
            ok
    end.
